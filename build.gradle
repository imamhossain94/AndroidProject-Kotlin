// AndroidProject-Kotlin version: v13.1
//Import configuration file
apply from : 'configs.gradle'

buildscript {

    //Import common Maven library configuration
    apply from : 'maven.gradle'

    repositories {
        // Alibaba Cloud Cloud Effect Warehouse (Gradle plug-in): https://maven.aliyun.com/mvn/guide
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
        addCommonMaven(repositories)
    }

    // Studio plug-in recommendation: https://github.com/getActivity/StudioPlugins
    dependencies {
        // Gradle plug-in version description: https://developer.android.google.cn/studio/releases/gradle-plugin.html#updating-plugin
        // noinspection GradleDependency
        classpath 'com.android.tools.build:gradle:4.1.2'
        // Kotlin plug-in: https://plugins.jetbrains.com/plugin/6954-kotlin
        // noinspection GradleDependency
        // classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.31'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.0'
        // AOP configuration plug-in: https://github.com/HujiangTechnology/gradle_plugin_android_aspectjx
        classpath 'com.hujiang.aspectjx:gradle-android-plugin-aspectjx:2.0.10'
    }
}

allprojects {

    repositories {
        // Umeng remote warehouse: https://info.umeng.com/detail?id=443&cateId=1
        maven { url 'https://repo1.maven.org/maven2' }
        addCommonMaven(repositories)
    }

    tasks.withType(JavaCompile).configureEach {
        //Set global encoding
        options.encoding = 'UTF-8'
    }
    tasks.withType(Javadoc).configureEach {
        //Set document encoding
        options {
            encoding 'UTF-8'
            charSet 'UTF-8'
            links 'http://docs.oracle.com/javase/7/docs/api'
        }
    }

    // Output the build files uniformly to the build folder in the project root directory
    setBuildDir(new File(rootDir, "build/${path.replaceAll(':', '/')}"))
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}