apply plugin : 'com.android.application'
apply plugin : 'android-aspectjx'
//apply plugin : 'kotlin-kapt'
apply from : '../common.gradle'
apply plugin: 'kotlin-kapt'

// Android code standard document: https://github.com/getActivity/AndroidCodeStandard
android {

    // Resource directory storage guide: https://developer.android.google.cn/guide/topics/resources/providing-resources
    defaultConfig {

        // Painlessly modify the package name: https://www.jianshu.com/p/17327e191d2e
        applicationId 'com.hjq.demo'

        // Only keep Chinese language resources
        resConfigs 'zh'

        // Only retain xxhdpi image resources (currently mainstream resolution 1920 * 1080)
        resConfigs 'xxhdpi'

        //Confusing configuration
        proguardFiles 'proguard-sdk.pro', 'proguard-app.pro'

        // Log printing switch
        buildConfigField('boolean', 'LOG_ENABLE', '' + LOG_ENABLE + '')
        // BuglyId under test package
        buildConfigField('String', 'BUGLY_ID', '"' + BUGLY_ID + '"')
        //The host address of the test server
        buildConfigField('String', 'HOST_URL', '"' + HOST_URL + '"')
    }

    // Things about Apk signature: https://www.jianshu.com/p/a1f8e5896aa2
    signingConfigs {
        config {
            storeFile file(StoreFile)
            storePassword StorePassword
            keyAlias KeyAlias
            keyPassword KeyPassword
        }
    }

    // Build configuration: https://developer.android.google.cn/studio/build/build-variants
    buildTypes {

        debug {
            //Add suffix to package name
            applicationIdSuffix '.debug'
            //Debug mode switch
            debuggable true
            jniDebuggable true
            //Compression alignment switch
            zipAlignEnabled false
            //Remove useless resources
            shrinkResources false
            // Code obfuscation switch
            minifyEnabled false
            // Signature information configuration
            signingConfig signingConfigs.config
            //Add list placeholder
            addManifestPlaceholders([
                    'app_name' : 'Android technology center Debug version'
            ])
            // In debug mode, only one architecture of the so library is retained to improve packaging speed.
            ndk {
                abiFilters 'armeabi-v7a'
            }
        }

        preview.initWith(debug)
        preview {
            applicationIdSuffix ''
            //Add list placeholder
            addManifestPlaceholders([
                    'app_name' : 'Android technology center Preview version'
            ])
        }

        release {
            //Debug mode switch
            debuggable false
            jniDebuggable false
            //Compression alignment switch
            zipAlignEnabled true
            //Remove useless resources
            shrinkResources true
            // Code obfuscation switch
            minifyEnabled true
            // Signature information configuration
            signingConfig signingConfigs.config
            //Add list placeholder
            addManifestPlaceholders([
                    'app_name' : '@string/app_name'
            ])
            // Only retain the so library of the two architectures, based on Bugly statistics
            ndk {
                // armeabi: Tiger Balm architecture platform (occupancy rate: 0%)
                // armeabi-v7a: Once a mainstream architecture platform (occupancy rate: 10%)
                // arm64-v8a: current mainstream architecture platform (occupancy rate: 95%)
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }
    }

    packagingOptions {
        // Remove all files under this package (signature information will not be removed)
        exclude 'META-INF/*******'
    }

    //AOP configuration (choose one of exclude and include)
    //Configuration is required, otherwise conflicts will occur, specifically as follows:
    // The first type: cannot be compiled, error is reported: java.util.zip.ZipException: Cause: zip file is empty
    // The second type: Compilation can pass, but an error occurs when running: ClassNotFoundException: Didn't find class on path: DexPathList
    aspectjx {
        // Exclude the package names of some third-party libraries (Gson, LeakCanary and AOP conflict)
        // exclude 'androidx', 'com.google', 'com.squareup', 'org.apache', 'com.alipay', 'com.taobao', 'versions.9'
        // Only perform AOP processing on the following package names
        include android.defaultConfig.applicationId
    }

    applicationVariants.all { variant ->
        // apk output file name configuration
        variant.outputs.all { output ->
            outputFileName = rootProject.getName() + '_v' + variant.versionName + '_' + variant.buildType.name
            if (variant.buildType.name == buildTypes.release.getName()) {
                outputFileName += '_' + new Date().format('MMdd')
            }
            outputFileName += '.apk'
        }
    }
}

//Add build dependencies: https://developer.android.google.cn/studio/build/dependencies
//The difference between api and implementation: https://www.jianshu.com/p/8962d6ba936e
dependencies {
    // Base class encapsulation
    implementation project(':library:base')
    // Control encapsulation
    implementation project(':library:widget')
    // Umeng package
    implementation project(':library:umeng')

    // Permission request framework: https://github.com/getActivity/XXPermissions
    implementation 'com.github.getActivity:XXPermissions:12.3'

    // Title bar framework: https://github.com/getActivity/TitleBar
    implementation 'com.github.getActivity:TitleBar:9.2'

    // Toast framework: https://github.com/getActivity/ToastUtils
    implementation 'com.github.getActivity:ToastUtils:9.5'

    // Network request framework: https://github.com/getActivity/EasyHttp
    implementation 'com.github.getActivity:EasyHttp:10.2'
    // OkHttp framework: https://github.com/square/okhttp
    // noinspection GradleDependency
    implementation 'com.squareup.okhttp3:okhttp:3.12.13'

    // Json parsing framework: https://github.com/google/gson
    implementation 'com.google.code.gson:gson:2.8.8'
    // Gson parsing fault tolerance: https://github.com/getActivity/GsonFactory
    implementation 'com.github.getActivity:GsonFactory:5.2'

    // Shape framework: https://github.com/getActivity/ShapeView
    implementation 'com.github.getActivity:ShapeView:6.0'

    // AOP plug-in library: https://mvnrepository.com/artifact/org.aspectj/aspectjrt
    implementation 'org.aspectj:aspectjrt:1.9.6'

    // Image loading framework: https://github.com/bumptech/glide
    // Official documentation: https://github.com/Muyangmin/glide-docs-cn
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'

    // Immersion framework: https://github.com/gyf-dev/ImmersionBar
    implementation 'com.gyf.immersionbar:immersionbar:3.0.0'

    // Gesture ImageView: https://github.com/Baseflow/PhotoView
    implementation 'com.github.Baseflow:PhotoView:2.3.0'

    // Bugly exception catching: https://bugly.qq.com/docs/user-guide/instruction-manual-android/?v=20190418140644
    implementation 'com.tencent.bugly:crashreport:3.4.4'
    implementation 'com.tencent.bugly:nativecrashreport:3.9.2'

    //Animation parsing library: https://github.com/airbnb/lottie-android
    // Animation resources: https://lottiefiles.com, https://icons8.com/animated-icons
    implementation 'com.airbnb.android:lottie:4.1.0'

    // Pull up to refresh and pull down to load the framework: https://github.com/scwang90/SmartRefreshLayout
    implementation 'com.scwang.smart:refresh-layout-kernel:2.0.3'
    implementation 'com.scwang.smart:refresh-header-material:2.0.3'

    // Log printing framework: https://github.com/JakeWharton/timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Indicator framework: https://github.com/ongakuer/CircleIndicator
    implementation 'me.relex:circleindicator:2.1.6'

    // Tencent MMKV: https://github.com/Tencent/MMKV
    implementation 'com.tencent:mmkv-static:1.2.10'

    // Memory leak monitoring framework: https://github.com/square/leakcanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'
    previewImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'

    // Multilingual: https://github.com/getActivity/MultiLanguages
    // Floating window: https://github.com/getActivity/XToast
    // Log output: https://github.com/getActivity/Logcat
    // Tool class: https://github.com/Blankj/AndroidUtilCode
    // Carousel image: https://github.com/bingoogolapple/BGABanner-Android
    // QR code: https://github.com/bingoogolapple/BGAQRCode-Android
    // Marquee View: https://github.com/sunfusheng/MarqueeView
    // Object annotation: https://www.jianshu.com/p/f1f888e4a35f
    // Object storage: https://github.com/leavesC/DoKV
    // Multi-channel packaging: https://github.com/Meituan-Dianping/walle
    //Unique device identifier: http://msa-alliance.cn/col.jsp?id=120
    // Nested scrolling container: https://github.com/donkingliang/ConsecutiveScroller
    // Privacy call monitoring: https://github.com/huage2580/PermissionMonitor
}