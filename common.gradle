apply plugin: 'kotlin-android'

//General configuration
android {

    //Compile source code version
    compileSdkVersion 30
    defaultConfig {
        // Minimum installation version
        minSdkVersion 21
        //Target adaptation version
        targetSdkVersion 30
        versionName '1.0'
        versionCode 10
    }

    //Support Java JDK 8
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    //Set the directory where so files are stored
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    // You can switch the default build type in the Build Variants option on the far left side of Studio
    buildTypes {
        //debug version
        debug {}
        // Preview version
        preview {}
        // official version
        release {}
    }

    // Code warning configuration
    lintOptions {
        // Disable text hardcoded warnings
        disable 'HardcodedText'
        // Disable image description warning
        disable 'ContentDescription'
    }
}

afterEvaluate {
    // The prerequisite is that this Module project must be of Library type, and the Module project named umeng is excluded.
    if (android.defaultConfig.applicationId == null && "umeng" != getName()) {
        //Exclude BuildConfig.class
        generateReleaseBuildConfig.enabled = false
        generatePreviewBuildConfig.enabled = false
        generateDebugBuildConfig.enabled = false
    }
}
dependencies {
    // Depend on all jar and aar packages in the libs directory
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')

    // AndroidX library: https://github.com/androidx/androidx
    implementation 'androidx.appcompat:appcompat:1.3.1'
    // Material library: https://github.com/material-components/material-components-android
    implementation 'com.google.android.material:material:1.4.0'

    // Kotlin coroutines: https://github.com/Kotlin/kotlinx.coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
}